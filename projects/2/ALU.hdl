// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
        // Zero x?
        Mux16(a = x[0..15], b = false, sel = zx, out = xint);    
            
        // Invert x
        Not16(in = xint, out= notx);

        // Select inverted x?
        Mux16(a = xint, b = notx, sel = nx, out = finalx);
        
        // Zero y?
        Mux16(a = y[0..15], b = false, sel = zy, out = yint);    
            
        // Invert y
        Not16(in = yint, out= noty);

        // Select inverted y?
        Mux16(a = yint, b = noty, sel = ny, out = finaly);
       
        // x + y or x & y
        And16(a = finalx, b = finaly, out = xandy);
        Add16(a = finalx, b = finaly, out = sumxy);

        Mux16(a=xandy, b = sumxy, sel = f, out=iout);

        // Invert result
        Not16(in=iout, out = notiout);

        // Select inverted result?
        Mux16(a = iout, b = notiout, sel = no, out=out, out[0..7]=outl, out[8..15]=outh, out[15]= ng);

        // zr
        Or8Way(in = outl, out = zrl);
        Or8Way(in = outh, out = zrm);
        Or(a = zrl, b = zrm, out = zri);
        Not(in = zri, out = zr);
}
